apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

String getLocalProperty(String name) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$projectDir/local.properties"))
    return properties.getProperty(name)
}

String getProperty(String filename, String name) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(filename))
    return properties.getProperty(name)
}

static String quoted(String input) {
    return "\"$input\""
}

android {

    compileSdkVersion = 31

    defaultConfig {

        applicationId "edu.cnm.deepdive.marblemaze"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'CLIENT_ID',
                quoted(getProperty("$projectDir\\..\\..\\..\\services\\marblemaze.properties", 'client_id'))

        resValue "string", "app_name", rootProject.name
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'room.schemaLocation': "$projectDir/schemas".toString()
                ]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    //noinspection GroovyAssignabilityCheck
    buildFeatures {
        //noinspection GroovyAssignabilityCheck
        viewBinding true
    }

}

dependencies {

    // .jar-based libraries included in project
    implementation fileTree(dir: "libs", include: ["*.jar"])

// Basic Android Support Libraries
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Material Design Components
    implementation 'com.google.android.material:material:1.4.0'

    // Preferences/settings library
    implementation 'androidx.preference:preference:1.1.1'

    // Navigation Libraries
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation "androidx.navigation:navigation-fragment:$navigationVersion"
    implementation "androidx.navigation:navigation-ui:$navigationVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

// Lifecycle (LiveData and ViewModel Libraries)
    def lifecycleVersion = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Room Libraries & Connectors
    def roomVersion = '2.3.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Gson Library
// OkHTTP Library
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.2'

    // ReactiveX Libraries
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Google Sign-In Library
    implementation 'com.google.android.gms:play-services-auth:19.2.0'

    // Stetho (Chromium inspection) Library
    implementation 'com.facebook.stetho:stetho:1.6.0'

    // Test Libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}